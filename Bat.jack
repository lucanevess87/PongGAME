class Bat {
	field int x, y;					// localização do retangulo
	field int width, height;	// largura e altura do retangulo
	field int direction;			// direcao esquerda (1) e direita (2) do retangulo

	constructor Bat new(int Ax, int Ay, int Awidth, int Aheight) {
		let x = Ax;
		let y = Ay;
		let width = Awidth;
		let height = Aheight;
		let direction = 2;     //direita - jogo começa com retangulo indo para direita
		do show();
		return this;
	}

	/** Abre espaço na memória jogando fora o que foi criado e não será mais utilizado */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** desenha e mostra retangulo. */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** desenha com a cor de fundo e consequentemente apaga retangulo */
    method void erase() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    /** desenha retangulo com largura e altura determinadas. */
    method void draw() {
    	do Screen.drawRectangle(x,y, x + width, y + height);
    	return;
    }


    /** direcao do retangulo (0=parado, 1=esquerda, 2=direita). */
    method void setDirection(int Adirection) {
    	let direction = Adirection;
    	return;
    }

    /** Returns the bat's left edge. */
    method int getLeft() {
        return x;
    }

    /** Returns the bat's right edge. */
    method int getRight() {
        return x + width;
    }

    /** proporciona apagar e desenhar retangulos maiores ou menores. */
    method void setWidth(int Awidth) {
    	do erase();
    	let width = Awidth;
    	do show();
    	return;
    }

    method void move() {
    	if (direction = 1) {                       // mover para esquerda
    		let x = x - 4;
            if (x < 0) { let x = 0; }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 4, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 3, y + height);
    	}

    	else {                                    // mover para direita
            let x = x + 4;
            if ((x + width) > 511) { let x = 511 - width; }
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);
        }

        return;
    }
}